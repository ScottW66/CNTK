# macros to include
load = ndlMnistMacros

# the actual NDL that defines the network
run = DNN

ndlMnistMacros = [
    featDim = 784
    labelDim = 10

    features = InputValue(featDim)
    featScale = Constant(0.00390625)
    featScaled = Scale(featScale, features)
    labels = InputValue(labelDim)
]

DNN = [
    hiddenDim = 200

    # DNNSigmoidLayer and DNNLayer are defined in Macros.ndl
    #h1 = DNNSigmoidLayer(featDim, hiddenDim, featScaled, 1)
    h1 = DNNLayer(featDim, hiddenDim, featScaled, 1)

    b = LearnableParameter(hiddenDim, 1, init = fixedValue, value = 0) 
    sc = LearnableParameter(hiddenDim, 1, init = fixedValue, value = 1) 
    m = LearnableParameter(hiddenDim, 1, init = fixedValue, value = 0, learningRateMultiplier = 0)
    isd = LearnableParameter(hiddenDim, 1, init = fixedValue, value = 0, learningRateMultiplier = 0)
    y = BatchNormalization(h1, sc, b, m, isd, eval=false, spatial=false, normalizationTimeConstant=64, imageLayout=cudnn, engine=cudnn)

    ol = DNNLayer(hiddenDim, labelDim, y, 1)

    ce = CrossEntropyWithSoftmax(labels, ol)
    errs = ErrorPrediction(labels, ol)
    top5Errs = ErrorPrediction(labels, ol, Const(5), tag="eval")  # only used in testing

    # Special Nodes
    FeatureNodes = (features)
    LabelNodes = (labels)
    CriterionNodes = (ce)
    EvalNodes = (errs)
    OutputNodes = (ol)
]
